*** Settings ***
Library           Selenium2Library
Resource          Project_URLs.txt
Resource          locators/Login_Page_Locators.txt
Resource          locators/Registration_Page_Locators.txt
Resource          locators/Start_Page_Locators.txt
Resource          locators/Inventory_Page_Locators.txt
Resource          locators/Navigation_Tree_Page_Locators.txt

*** Variables ***
${BROWSER}        Chrome

*** Keywords ***
New User Registration
    [Arguments]    ${USERNAME}    ${PASSWORD}    ${REPASSWORD}    ${EMAIL}    ${ROLE}
    Go To    ${REGISTRATION_PAGE_URL}
    Input Text    ${USERNAME_FIELD}    ${USERNAME}
    Input Password    ${PASSWORD_FIELD}    ${PASSWORD}
    Input Password    ${REPASSWORD_FIELD}    ${REPASSWORD}
    Input Text    ${EMAIL_FIELD}    ${EMAIL}
    Select From List By Value    ${ROLE_LIST}    ${ROLE}
    Click Button    ${CONFIRM_REGISTRATION_BUTTON}

Login
    [Arguments]    ${USERNAME}    ${PASSWORD}
    Go To    ${LOGIN_PAGE_URL}
    Input Text    ${L_USERNAME_FIELD}    ${USERNAME}
    Input Password    ${L_PASSWORD_FIELD}    ${PASSWORD}
    Submit Form    ${L_SUBMIT}

Logout
    Click Link    ${LOGOUT_LINK}

Create Country
    [Arguments]    ${COUNTRY_NAME}    ${COUNTRY_CONTINENT}    ${COUNTRY_LANGUAGE}
    Click Link    ${CREATE_COUNTRY_LINK}
    Input Text    ${COUNTRY_NAME_FIELD}    ${COUNTRY_NAME}
    Select From List By Value    ${COUNTRY_CONTINENT_LIST}    ${COUNTRY_CONTINENT}
    Input Text    ${COUNTRY_LANGUAGE_FIELD}    ${COUNTRY_LANGUAGE}
    Click Button    ${COUNTRY_SAVE_BUTTON}

Open Country
    [Arguments]    ${COUNTRY_NAME}
    Go To    ${INVENTORY_PAGE_URL}
    Click Link    xpath=//*[contains(text(), '${COUNTRY_NAME}')]

Create City
    [Arguments]    ${CITY_NAME}    ${CITY_POPULATION}    ${CITY_IS_REGIONAL_CENTER}
    Click Link    ${CREATE_CITY_LINK}
    Input Text    ${CITY_NAME_FIELD}    ${CITY_NAME}
    Input Text    ${CITY_POPULATION_FIELD}    ${CITY_POPULATION}
    Select From List By Value    ${CITY_IS_REGIONAL_CENTER_LIST}    ${CITY_IS_REGIONAL_CENTER}
    Click Button    ${CITY_SAVE_BUTTON}

Open City
    [Arguments]    ${CITY_NAME}
    Click Link    ${CITIES_TAB}
    Click Link    xpath=//*[contains(text(), '${CITY_NAME}')]

Create Building
    [Arguments]    ${BUILDING_NAME}    ${BUILDING_STREET_NAME}    ${BUILDING_NUMBER}    ${BUILDING_SQUARE}    ${BUILDING_IS_CONNECTED}
    Click Link    ${CREATE_BUILDING_LINK}
    Input Text    ${BUILDING_NAME_FIELD}    ${BUILDING_NAME}
    Input Text    ${BUILDING_STREET_NAME_FIELD}    ${BUILDING_STREET_NAME}
    Input Text    ${BUILDING_NUMBER_FIELD}    ${BUILDING_NUMBER}
    Input Text    ${BUILDING_SQUARE_FIELD}    ${BUILDING_SQUARE}
    Select From List By Value    ${BUILDING_IS_CONNECTED_LIST}    ${BUILDING_IS_CONNECTED}
    Click Button    ${BUILDING_SAVE_BUTTON}

Open Building
    [Arguments]    ${BUILDING_NAME}
    Click Link    ${BUILDINGS_TAB}
    Click Link    xpath=//*[contains(text(), '${BUILDING_NAME}')]

Create Floor
    [Arguments]    ${FLOOR_NUMBER}    ${FLOOR_SQUARE}
    Click Link    ${CREATE_FLOOR_LINK}
    Input Text    ${FLOOR_NUMBER_FIELD}    ${FLOOR_NUMBER}
    Input Text    ${FLOOR_SQUARE_FIELD}    ${FLOOR_SQUARE}
    Click Button    ${FLOOR_SAVE_BUTTON}

Open Floor
    [Arguments]    ${FLOOR_NUMBER}
    Click Link    ${FLOORS_TAB}
    Click Link    xpath=//*[contains(text(), 'Floor#${FLOOR_NUMBER}')]

Create Room
    [Arguments]    ${ROOM_NAME}    ${ROOM_SQUARE}
    Click Link    ${CREATE_ROOM_LINK}
    Input Text    ${ROOM_NAME_FIELD}    ${ROOM_NAME}
    Input Text    ${ROOM_SQUARE_FIELD}    ${ROOM_SQUARE}
    Click Button    ${ROOM_SAVE_BUTTON}

Open Room
    [Arguments]    ${ROOM_NAME}
    Click Link    ${ROOMS_TAB}
    Click Link    xpath=//*[contains(text(), '${ROOM_NAME}')]

Create Rack
    [Arguments]    ${RACK_NAME}    ${RACK_WIDTH}    ${RACK_LENGTH}    ${RACK_HEIGHT}    ${RACK_PHYSICAL_STATUS}
    Click Link    ${CREATE_RACK_LINK}
    Input Text    ${RACK_NAME_FIELD}    ${RACK_NAME}
    Input Text    ${RACK_WIDTH_FIELD}    ${RACK_WIDTH}
    Input Text    ${RACK_LENGTH_FIELD}    ${RACK_LENGTH}
    Input Text    ${RACK_HEIGHT_FIELD}    ${RACK_HEIGHT}
    Select From List By Value    ${RACK_PHYSICAL_STATUS_LIST}    ${RACK_PHYSICAL_STATUS}
    Click Button    ${RACK_SAVE_BUTTON}

Open Rack
    [Arguments]    ${RACK_NAME}
    Click Link    ${RACKS_TAB}
    Click Link    xpath=//*[contains(text(), '${RACK_NAME}')]

Create Device
    [Arguments]    ${DEVICE_NAME}    ${DEVICE_MAC_ADDRESS}    ${DEVICE_RAM}    ${DEVICE_CPU}    ${DEVICE_IP_ADDRESS}    ${DEVICE_PHYSICAL_STATUS}
    ...    ${DEVICE_WIDTH}    ${DEVICE_LENGTH}    ${DEVICE_HEIGHT}
    Click Link    ${CREATE_DEVICE_LINK}
    Input Text    ${DEVICE_NAME_FIELD}    ${DEVICE_NAME}
    Input Text    ${DEVICE_MAC_ADDRESS_FIELD}    ${DEVICE_MAC_ADDRESS}
    Input Text    ${DEVICE_RAM_FIELD}    ${DEVICE_RAM}
    Input Text    ${DEVICE_CPU_FIELD}    ${DEVICE_CPU}
    Input Text    ${DEVICE_IP_ADDRESS_FIELD}    ${DEVICE_IP_ADDRESS}
    Select From List By Value    ${DEVICE_PHYSICAL_STATUS_LIST}    ${DEVICE_PHYSICAL_STATUS}
    Input Text    ${DEVICE_WIDTH_FIELD}    ${DEVICE_WIDTH}
    Input Text    ${DEVICE_LENGTH_FIELD}    ${DEVICE_LENGTH}
    Input Text    ${DEVICE_HEIGHT_FIELD}    ${DEVICE_HEIGHT}
    Click Link    ${DEVICE_LOCATED_IN_SELECT_LINK}
    Choose From Navigation Tree
    Click Link    ${DEVICE_NETWORK_ELEMENT_SELECT_LINK}
    Choose From Navigation Tree
    Click Button    ${DEVICE_SAVE_BUTTON}

Choose From Navigation Tree
    Select Window    title=Navigation Tree
    Click Link    ${COUNTRY_BRANCH}
    Click Button    ${OK_BUTTON}
    Select Window

Create ATM
    [Arguments]    ${ATM_NAME}    ${ATM_CONNECTION_TYPE}    ${ATM_EXTRA_SECURITY}    ${ATM_PHYSICAL_STATUS}
    Click Link    ${CREATE_ATM_LINK}
    Input Text    ${ATM_NAME_FIELD}    ${ATM_NAME}
    Input Text    ${ATM_CONNECTION_TYPE_FIELD}    ${ATM_CONNECTION_TYPE}
    Input Text    ${ATM_EXTRA_SECURITY_FIELD}    ${ATM_EXTRA_SECURITY}
    Select From List By Value    ${ATM_PHYSICAL_STATUS_LIST}    ${ATM_PHYSICAL_STATUS}
    Click Link    ${ATM_LOCATED_IN_SELECT_LINK}
    Choose From Navigation Tree
    Click Button    ${ATM_SAVE_BUTTON}

Go To ATM Tab
    [Arguments]    ${COUNTRY_NAME}    ${CITY_NAME}    ${BUILDING_NAME}
    Go To    ${INVENTORY_PAGE_URL}
    Click Link    xpath=//*[contains(text(), '${COUNTRY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${CITY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${BUILDING_NAME}')]
    Click Link    ${ATMs_TAB}
    Set Browser Implicit Wait    3

Go To Device Tab
    [Arguments]    ${COUNTRY_NAME}    ${CITY_NAME}    ${BUILDING_NAME}    ${FLOOR_NUMBER}    ${ROOM_NAME}    ${RACK_NAME}
    Go To    ${INVENTORY_PAGE_URL}
    Click Link    xpath=//*[contains(text(), '${COUNTRY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${CITY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${BUILDING_NAME}')]
    Click Link    xpath=//*[contains(text(), 'Floor#${FLOOR_NUMBER}')]
    Click Link    xpath=//*[contains(text(), '${ROOM_NAME}')]
    Click Link    xpath=//*[contains(text(), '${RACK_NAME}')]
    Set Browser Implicit Wait    3

Go To Pay Box Tab
    [Arguments]    ${COUNTRY_NAME}    ${CITY_NAME}    ${BUILDING_NAME}
    Go To    ${INVENTORY_PAGE_URL}
    Click Link    xpath=//*[contains(text(), '${COUNTRY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${CITY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${BUILDING_NAME}')]
    Click Link    ${PAY_BOXES_TAB}
    Set Browser Implicit Wait    3

Create Pay Box
    [Arguments]    ${PAY_BOX_NAME}    ${PAY_BOX_SECURE_PROTOCOL}    ${PAY_BOX_DISPLAY_SIZE}    ${PAY_BOX_PHYSICAL_STATUS}
    Click Link    ${CREATE_PAY_BOX_LINK}
    Input Text    ${PAY_BOX_NAME_FIELD}    ${PAY_BOX_NAME}
    Input Text    ${PAY_BOX_SECURE_PROTOCOL_FIELD}    ${PAY_BOX_SECURE_PROTOCOL}
    Input Text    ${PAY_BOX_DISPLAY_SIZE_FIELD}    ${PAY_BOX_DISPLAY_SIZE}
    Select From List By Value    ${PAY_BOX_PHYSICAL_STATUS_LIST}    ${PAY_BOX_PHYSICAL_STATUS}
    Click Link    ${PAY_BOX_LOCATED_IN_SELECT_LINK}
    Choose From Navigation Tree
    Click Button    ${PAY_BOX_SAVE_BUTTON}

Go To Post Terminal Tab
    [Arguments]    ${COUNTRY_NAME}    ${CITY_NAME}    ${BUILDING_NAME}
    Go To    ${INVENTORY_PAGE_URL}
    Click Link    xpath=//*[contains(text(), '${COUNTRY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${CITY_NAME}')]
    Click Link    xpath=//*[contains(text(), '${BUILDING_NAME}')]
    Click Link    ${POST_TERMINALS_TAB}
    Set Browser Implicit Wait    3

Create Post Terminal
    [Arguments]    ${POST_TERMINAL_NAME}    ${POST_TERMINAL_WIDTH}    ${POST_TERMINAL_LENGTH}    ${POST_TERMINAL_HEIGHT}    ${POST_TERMINAL_PHYSICAL_STATUS}
    Click Link    ${CREATE_POST_TERMINAL_LINK}
    Input Text    ${POST_TERMINAL_NAME_FIELD}    ${POST_TERMINAL_NAME}
    Input Text    ${POST_TERMINAL_WIDTH_FIELD}    ${POST_TERMINAL_WIDTH}
    Input Text    ${POST_TERMINAL_LENGTH_FIELD}    ${POST_TERMINAL_LENGTH}
    Input Text    ${POST_TERMINAL_HEIGHT_FIELD}    ${POST_TERMINAL_HEIGHT}
    Select From List By Value    ${POST_TERMINAL_PHYSICAL_STATUS_LIST}    ${POST_TERMINAL_PHYSICAL_STATUS}
    Click Link    ${POST_TERMINAL_LOCATED_IN_SELECT_LINK}
    Choose From Navigation Tree
    Click Button    ${POST_TERMINAL_SAVE_BUTTON}

Delete Country
    [Arguments]    ${COUNTRY_NAME}
    Click Element    //div[@id='table_data']//td[a='${COUNTRY_NAME}']/input
    Click Button    ${DELETE_COUNTRY_BUTTON}
